 10 POKE546,47:POKE547,13:POKE548,208:POKE549,205:POKE550,211
 15 GOSUB10000
 20 POKE550,215:POKE56960,3
 25 CLEAR
 30 TL=53269:BL=TL+30*64:TR=TL+30:BR=BL+30
 35 READLD:LD=LD-1:DIMVL(LD)
 40 FORI=LDTO0STEP-1:READVL(I):NEXT
 50 DEFFNR(X)=INT(RND(23)*X+1)
 100 PRINTCHR$(26)
 110 XD=FNR(19)+6:YD=FNR(19)+6
 120 DIMMZ(XD+1,YD+1)
 130 NF=INT(XD*YD*.29)
 150 X=INT(XD/2):Y=INT(YD/2)
 160 MZ(X,Y)=-1
 170 PRINT:PRINTTAB(5)"Producing the random maze..."
 180 PRINTCHR$(12):PRINT:PRINT:PRINT:PRINTTAB(22);NF
 185 DR=FNR(4):XX=0:YY=0:P=-1:GOTO210
 190 IFFNR(100)>50THENP=1:GOTO210
 200 DR=FNR(4):P=-1
 210 IFDR=1THENXX=2:YY=0:GOTO250
 220 IFDR=2THENXX=-2:YY=0:GOTO250
 230 IFDR=3THENYY=2:XX=0:GOTO250
 240 YY=-2:XX=0
 250 IFX+XX<1ORY+YY<1ORX+XX>XDORY+YY>YDTHEN290
 255 IFMZ(X+XX/2,Y+YY/2)<>0THEN290
 260 MZ(X+XX,Y+YY)=P:MZ(X+XX/2,Y+YY/2)=1:X=X+XX:Y=Y+YY
 270 NF=NF-1:IFNF<=0THEN330
 280 GOTO190
 290 MZ(X,Y)=-1
 300 FORI=FNR(XD)TO1STEP-1:FORJ=FNR(YD)TO1STEP-1
 310 IFMZ(I,J)=-1THENX=I:Y=J:GOTO180
 320 NEXTJ,I:GOTO300
 330 FORI=2TO3
 335 X=FNR(XD):Y=FNR(YD)
 340 IFMZ(X,Y)=0ORMZ(X,Y)=-2THEN335
 350 MZ(X,Y)=-I:NEXT
 360 YY=0:XX=FNR(2)-1:IFXX=0THENYY=1
 400 GOSUB6000
 410 PRINT:PRINTCHR$(12);:POKE512,15:GU=-1
 415 PRINT"Which Direction ?";
 420 POKE11,0:POKE12,253:X=USR(X):PK=PEEK(531)
 430 IFPK<>32THEN470
 440 IFMZ(X+XX,Y+YY)=0THEN410
 445 IFMZ(X,Y)<>-3THENMZ(X,Y)=2
 450 X=X+XX:Y=Y+YY:IFMZ(X,Y)=-2THENGU=0:GOTO560
 460 IFMZ(X,Y)<>-3THENMZ(X,Y)=5
 465 TN=TN+1:GOTO400
 470 IFPK<>66THEN510
 480 IFMZ(X-XX,Y-YY)=0THEN410
 485 IFMZ(X,Y)<>-3THENMZ(X,Y)=2
 490 X=X-XX:Y=Y-YY:IFMZ(X,Y)=-2THENGU=0:GOTO560
 500 IFMZ(X,Y)<>-3THENMZ(X,Y)=5
 505 TN=TN+1:GOTO400
 510 IFPK<>44THEN540
 520 IFXX<>0THENYY=-XX:XX=0:GOTO400
 530 XX=YY:YY=0:GOTO400
 540 IFPK<>46THEN558
 550 IFYY<>0THENXX=-YY:YY=0:GOTO400
 555 YY=XX:XX=0:GOTO400
 558 IFPK<>71THEN410
 560 PRINTCHR$(26)
 570 FORI=0TOYD+1:FORJ=0TOXD+1
 580 IFMZ(J,I)=0THENPRINTCHR$(161);:GOTO650
 590 IFMZ(J,I)=2THENPRINTCHR$(213);:GOTO650
 600 IFMZ(J,I)=-2THENPRINTCHR$(187);:GOTO650
 610 IFMZ(J,I)=-3THENPRINTCHR$(14);:GOTO650
 615 IFMZ(J,I)=5THENPRINTCHR$(42);:GOTO650
 620 PRINT" ";
 650 NEXT:PRINT:NEXT
 660 PRINT:IFGUTHENPRINT"So far you have made";:GOTO670
 665 PRINT"That took you";:TN=TN+1
 670 PRINTTN;"move";:IFTN<>1THENPRINT"s":GOTO680
 675 PRINT
 680 IFNOTGUTHEN700
 690 PRINT" - Press 'C' to continue"
 695 POKE11,0:POKE12,253:X=USR(X)
 697 IFPEEK(531)=67THEN400
 699 FORI=1TO300:NEXT
 700 PRINT"Press any key for a new maze"
 705 POKE530,1:POKE57088,0:PK=PEEK(57088)
 710 IFPEEK(57088)=PKTHEN710
 720 POKE530,0:RUN20
 6000 PRINTCHR$(26):PS=-1
 6010 FORLV=LDTO0STEP-1:PV=PS+1:PS=PS+VL(LV)
 6020 PX=X+(LD-LV)*XX:PY=Y+(LD-LV)*YY:P1=MZ(PX,PY)
 6030 L1=MZ(PX+YY,PY-XX):R1=MZ(PX-YY,PY+XX)
 6040 IFL1=0THENGOSUB7000:GOTO6050
 6045 GOSUB8000
 6050 IFR1=0THENGOSUB7500:GOTO6060
 6055 GOSUB8500
 6060 P2=MZ(PX+XX,PY+YY)
 6065 L2=MZ(PX+XX+YY,PY+YY-XX):R2=MZ(PX+XX-YY,PY+YY+XX)
 6070 IFL1=0ANDP2=0THENGOSUB9000
 6080 IFR1=0ANDP2=0THENGOSUB9500
 6090 IFP2=0THEN6200
 6100 IFL1OR(L1=0ANDL2)THENGOSUB9000
 6110 IFR1OR(R1=0ANDR2)THENGOSUB9500
 6112 IFP1=-2THENGOSUB6800
 6120 NEXT
 6122 POKETL+975,63
 6125 IFP2=-2THENPOKETL+975,161
 6130 FORZ=0TO30:Z1=64*Z
 6140 POKEBL+64+Z,135:POKETL+Z1-1,143:POKETR+Z1+1,136
 6150 NEXT:RETURN
 6200 FORZ=PS+1TO29-PS
 6210 POKETL+Z+PS*64,128:POKEBL+Z-PS*64,135
 6220 NEXT:IFP1=-2THENGOSUB6800
 6230 GOTO6130
 6800 PT=PV-1:FORZ=PVTOPS
 6805 IFPT>13THENPOKEBL+PT+1-64*Z,161:GOTO6840
 6810 POKEBL+PT+1-64*Z,176:POKEBL+29-PT-64*Z,178
 6820 FORZ1=PT+2TO28-PT:POKEBL+Z1-64*Z,161
 6830 NEXT:PT=PT+1
 6840 NEXT:RETURN
 7000 FORZ=PVTOPS:ZZ=Z*64
 7010 POKETL+ZZ+Z,190:POKEBL-ZZ+Z,189
 7020 NEXT:RETURN
 7500 FORZ=PVTOPS:ZZ=Z*64
 7510 POKETR+ZZ-Z,189:POKEBR-ZZ-Z,190
 7520 NEXT:RETURN
 8000 ZZ=PS*64:FORZ=PVTOPS
 8010 POKETL+ZZ+Z,128:POKEBL-ZZ+Z,135
 8020 NEXT:RETURN
 8500 ZZ=PS*64:FORZ=PVTOPS
 8510 POKETR+ZZ-Z,128:POKEBR-ZZ-Z,135
 8520 NEXT:RETURN
 9000 FORZ=PS+1TO29-PS
 9010 POKETL+64*Z+PS,143
 9020 NEXT:RETURN
 9500 FORZ=PS+1TO29-PS
 9510 POKETR+64*Z-PS,136
 9520 NEXT:RETURN
 10000 POKE56960,10:PRINTCHR$(26)
 10010 PRINT:PRINT:PRINTTAB(18)"LABYRINTH"
 10020 PRINTTAB(18);:FORI=1TO9:PRINTCHR$(134);:NEXT:PRINT
 10030 FORI=1TO2000:NEXT
 10040 PRINT::PRINT"You are lost in a random maze, and
 10050 PRINT"you must find the exit square to escape."
 10060 PRINT"Press 'SPACE' to move forwards,"
 10070 PRINT"Press   'B'   to move backwards,"
 10080 PRINT"Press   '<'   to turn left and"
 10090 PRINT"Press   '>'   to turn right."
 10100 PRINT:PRINT"The exit is marked by ";
 10101 PRINTCHR$(161)CHR$(161)CHR$(161)CHR$(161);
 10102 PRINT" on the floor."
 10105 PRINTTAB(22)CHR$(161)CHR$(161)CHR$(161)CHR$(161)
 10110 PRINT"Press any key to begin":PRINT
 10120 POKE530,1:POKE57088,0:PK=PEEK(57088)
 10130 IFPEEK(57088)=PKTHEN10130
 10140 POKE530,0:RETURN
 20000 DATA7,3,4,3,2,1,1,1
 